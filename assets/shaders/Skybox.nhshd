#CONFIG
cull=NONE
front=CLOCKWISE
fill=SOLID
depth=LESS_EQUAL
blend=ADD
useIndices=FALSE
useVertices=FALSE
topology=TRIANGLE_STRIP
vertexCount=4
#CONFIG_END

#VERTEX
#version 450

struct SkyboxData
{
	mat4 invViewProjection;
	uint skyboxIndex;
};

layout(push_constant) uniform block
{
	SkyboxData skyboxData;
};

layout (location = 0) out vec3 outTexCoord;
layout (location = 1) flat out uint outSkyboxIndex;

void main()
{
	vec2 pos = vec2((gl_VertexIndex & 2) >> 1, 1 - (gl_VertexIndex & 1)) * 2 - 1;

	vec4 clipSpaceNear = skyboxData.invViewProjection * vec4(pos, -1.0, 1.0);
	vec4 clipSpaceFar = skyboxData.invViewProjection * vec4(pos, 1.0, 1.0);

	vec3 nearPlaneVert = clipSpaceNear.xyz / clipSpaceNear.w;
	vec3 farPlaneVert = clipSpaceFar.xyz / clipSpaceFar.w;

	outTexCoord = farPlaneVert - nearPlaneVert;
	outSkyboxIndex = skyboxData.skyboxIndex;
	gl_Position = vec4(pos, 1.0, 1.0);
}
#VERTEX_END

#FRAGMENT
#version 450
#extension GL_EXT_nonuniform_qualifier : require

const uint INVALID_TEXTURE_INDEX = 65535;

layout (location = 0) in vec3 texCoord;
layout (location = 1) flat in uint skyboxIndex;

layout (set = 1, binding = 10) uniform samplerCube globalTexturesCubes[];

layout (location = 0) out vec4 fragColor;

void main()
{
	if(skyboxIndex != INVALID_TEXTURE_INDEX)
	{
		fragColor = texture(globalTexturesCubes[skyboxIndex], texCoord);
	}
}
#FRAGMENT_END